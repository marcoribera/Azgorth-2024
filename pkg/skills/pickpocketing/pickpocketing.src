/* $Id: stealing.src 1053 2007-04-27 22:00:25Z tekproxy $
 *
 *
 * http://uo.stratics.com/content/skills/stealing.shtml
 *
 */

use uo;
use os;

include ":attributes:attributes";
include "include/facings";
include "include/say";

program skill_Stealing(mobile)
	if ( GetProcess(CInt(GetObjProperty(mobile, "#PickPid"))) )
		SendSysMessageEx(mobile, "Voce ja esta roubando alguem.", SSM_FAIL);
		return;
	else
		SetObjProperty(mobile, "#PickPid", GetPid());
	endif

	if ( CInt(GetObjProperty(mobile, "#StealWait")) > ReadGameClock() )
		SendSysMessageEx(mobile, "Voce deve aguardar um pouco para usar essa pericia novamente.", SSM_INFO);
		return 0;
	else
		SetObjProperty(mobile, "#StealWait", ReadGameClock()+15);
	endif

	if (!maolivre(mobile, 1))
		return 0;
	endif

	SendSysMessageEx(mobile, "Selecione que pessoa ou item voce deseja roubar.", SSM_REQUEST);
	var targ := Target(mobile);

	if ( GetObjProperty(mobile, "kit") == "Bandido")
		if ( (targ.npctemplate == ":brainai:MercenarioRecruta") || (targ.npctemplate == ":brainai:MercenarioBesteiro") || (targ.npctemplate == ":brainai:Roguearcher") || (targ.npctemplate == ":brainai:Mercenario") || (targ.npctemplate == ":brainai:Rogue") || (targ.npctemplate == ":brainai:MercenarioLider") || (targ.npctemplate == ":brainai:MercenarioExperiente") )
			SendSysMessageEx(mobile, "Seu acordo te proibe roubar esta pessoa", SSM_FAIL);
			return 0;
		endif
	endif

	if (targ == mobile)
		SendSysMessageEx(mobile, "Voce chega a conclusao de que consegue abrir e fechar sua bolsa de forma normal.", SSM_FAIL);
		return 0;
	elseif ( targ.connected == 0 )
		SendSysMessageEx(mobile, "Voce nao pode roubar jogadores desconectados.", SSM_FAIL);
		return 0;
	//elseif ( targ.npctemplate )
	//	SendSysMessageEx(mobile, "Voce so pode roubar jogadores.", SSM_FAIL);
	//	return 0;
	//elseif ( targ.warmode && targ.npctemplate )
	//	SendSysMessageEx(mobile, "O alvo esta se movimentando demais lutando.", SSM_FAIL);
	//	return 0;
	elseif (!targ.IsA(POLCLASS_MOBILE))
		SendSysMessageEx(mobile, "Voce so pode roubar jogadores.", SSM_FAIL);
		return 0;
	elseif (targ.npctemplate == ":merchants:estalagem")
		SendSysMessageEx(mobile, "Voce nao pode roubar este tipo de NPC.", SSM_FAIL);
		return 0;
	elseif ( Distance(mobile, targ) > 2 )
		SendSysMessageEx(mobile, "O alvo esta muito longe.", SSM_FAIL);
		return 0;
	elseif ( !IsBehind(targ, mobile.x, mobile.y) )
		SendSysMessageEx(mobile, "Voce precisa estar atras do alvo que deseja roubar.", SSM_FAIL);
		return 0;
	endif

	var chance := AP_GetSkill(mobile, PICKPOCKETING);

	if (temHabilidade(mobile, "Punga"))
		chance:= chance + 50;
	endif

	if (RandomInt(100)+1 > chance) //Errou!
		SendSysMessageEx(mobile, "Voce falhou em abrir a bolsa do alvo", SSM_FAIL);
		return 0;
	endif

	var diff := 15 + CInt(AP_GetSkill(targ, AWARENESS));
	var check := SkillCheck(mobile, PICKPOCKETING, diff);

	if ( check > 0 )
		SendSysMessageEx(mobile, "Voce conseguiu abrir a mochila de "+targ.name+".", SSM_INFO);
		SendOpenSpecialContainer(mobile, targ.backpack);
		SendSysMessageEx(mobile, "Se desejar parar de roubar, pressione Esc no momento da escolha do item.", SSM_INFO);

		while (1)
			SendSysMessageEx(mobile, "Escolha um item para roubar.", SSM_REQUEST);
			var steal := Target(mobile);//, TGTOPT_NOCHECK_LOS);
			if (!steal)
				SendSysMessageEx(mobile, "Voce parou de roubar.", SSM_INFO);
				break;
			elseif ( Distance(mobile, targ) > 2 )
				SendSysMessageEx(mobile, "O alvo se afastou demais.", SSM_FAIL);
				break;
			elseif ( !IsBehind(targ, mobile.x, mobile.y) )
				SendSysMessageEx(mobile, "O alvo nao esta mais de costas para voce.", SSM_FAIL);
				break;
			elseif (steal == targ)
				SendSysMessageEx(mobile, "Alvo invalido.", SSM_FAIL);
				continue;
			endif
			var aux := steal;
			while (aux.container)
				aux := aux.container;
			endwhile
			if (aux != targ)
				SendSysMessageEx(mobile, "Voce so pode roubar da bolsa que abriu.", SSM_FAIL);
				continue;
			elseif (steal.IsA(POLCLASS_CONTAINER) && steal.objtype != 0x1011)
				if (steal.locked)
					SendSysMessageEx(mobile, steal.desc+" esta trancado.", SSM_FAIL);
				else
					SendViewContainer(mobile, steal);
					SendSysMessageEx(mobile, "Voce abriu "+steal.desc+".", SSM_INFO);
				endif
				continue;
			endif

			if (steal.objtype IN {0xba63, 0xba64, 0xba65}) // moeda
				diff := CInt(steal.amount / 5);
				if (temHabilidade(mobile, "Punga"))
					diff := diff /2;
				endif
			elseif (steal.objtype IN {0x1085, 0x1086, 0x1087, 0x1088, 0x1089, 0x108a, 0x2b6e, 0x2b6f, 0x2b70}) // joias
				diff := CInt(steal.weight*10);
				if (temHabilidade(mobile, "Punga"))
					diff := diff /2;
				endif
			elseif (steal.objtype IN {0x1011, 0x100E, 0x100F, 0x1010, 0x1012, 0x1013}) // chaves
				diff := 60;
				if (temHabilidade(mobile, "Punga"))
					diff := diff /2;
				endif
			elseif (steal.objtype == 0x6100 || steal.objtype == 0x6149 || steal.objtype == 0xec29 ) // grim�rio
				diff := 80;
			else
				diff := CInt(steal.weight*10);
			endif

			if (diff > 1.5 * AP_GetSkill(mobile, PICKPOCKETING))
				SendSysMessageEx(mobile, "Voce nao tentara roubar isso, pois tem certeza que falhara.", SSM_INFO);
				continue;
			endif

			var check := SkillCheck(mobile, PICKPOCKETING, diff);

			if ( check > 0 )
				MoveObjectToLocation(steal, 0, 0, 0, mobile.realm);
				MoveItemToContainer(steal, mobile.backpack);
				SendSysMessageEx(mobile, "Voce roubou "+steal.desc+".", SSM_INFO);
				SkillCheck(mobile, PICKPOCKETING, -1);
				diff:= diff +5;
			else
				SendSysMessageEx(mobile, "Voce nao conseguiu roubar "+steal.desc+".", SSM_FAIL);
				if ( check < -25 )
					CriticalFailure(mobile, targ);
				endif
				break;
			endif
		endwhile
		mobile.facing := mobile.facing; // Pra fechar o container! ;D
	else
		SendSysMessageEx(mobile, "Voce nota que o alvo percebeu algo.", SSM_FAIL);
		SendSysMessageEx(targ, "Voce sentiu algo na sua bolsa.", SSM_INFO);
		if (check < -10) // Falha Cr�tica
			CriticalFailure(mobile, targ);
		endif
	endif
endprogram

function CriticalFailure(mobile, targ)
	if (mobile.hidden)
		mobile.hidden := 0;
	endif
	SendSysMessageEx(mobile, "Voce chamou muita atencao!", SSM_FAIL);

	var last_loc := struct{ "x" := mobile.x, "z" := mobile.z, "y" := mobile.y };
	var range := 25 - CInt(AP_GetSkill(mobile, PICKPOCKETING)/5);

	foreach monster in ListMobilesNearLocationEx(targ.x, targ.y, targ.z, range, LISTEX_FLAG_NORMAL|LISTEX_FLAG_HIDDEN|LISTEX_FLAG_NPC_ONLY)
		if (GetObjProperty(monster, "guild"))
			SendEvent(monster, struct{ "type" := 0x78459, source := mobile, "last_loc" := last_loc});
		endif
		sleepms(2);
	endforeach

	foreach player in ( ListMobilesInLineOfSight(targ, range) )
		if ( player.npctemplate );
		elseif ( player == mobile );
		elseif ( player == targ );
		elseif ( !IsBehind(player, targ.x, targ.y) )
			SendSysMessageEx(player, "Voce nota "+mobile.name+" tentando roubar de "+targ.name+".", SSM_INFO);
		endif
		SleepMS(2);
	endforeach

	return 1;
endfunction
