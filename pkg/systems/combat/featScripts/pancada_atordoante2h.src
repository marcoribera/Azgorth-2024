use uo;
use os;
use cfgfile;

include "include/client";
include "include/sounds";
include "include/say";
include "include/damage";
include ":attributes:attributes";
include ":timedscripts:timedScripts";
include ":combat:combat";
include "include/facings";

program defensor(params)
	var who := params[1];
	//var spellinfo := params[2];
	//var targ := params[3];
	params := 0; // No longer needed

   if (TS_GetCooldown(who, "habilidade um"))
		return SendSysMessageEX(who, "Habilidade em Cooldown!", SSM_FAIL);
	endif

   var combatskill := AP_GetSkill(who, MELEECOMBAT2H);
   var armainicial := who.weapon;

   if ( combatskill < 20.0)
		SendSysMessageEx(who, "Voce nao tem conhecimento suficiente para executar o golpe", SSM_FAIL);
		return 0;
   endif

   SendSysMessageEx(who, "Quem deseja tombar?", SSM_INFO);
	var targ := Target(who);

   if (!targ)
      SendSysMessageEx(who, "Cancelado.", SSM_FAIL);
      return 1;
   elseif (!CheckLOSAt(who, targ.x, targ.y, targ.z))
		SendSysMessageEx(who, "O Alvo nao esta visivel para voce.", SSM_FAIL);
		return 1;
	elseif (CoordinateDistance(who.x, who.y, targ.x, targ.y) > 1)
		SendSysMessageEx(who, "O Alvo esta muito distante.", SSM_FAIL);
		return 1;
	elseif (IsBehind(who, targ.x, targ.y))
		SendSysMessageEx(who, "Voce esta de costas para o alvo!", SSM_FAIL);
		return 1;
	elseif ( who.weapon != armainicial )
		SendSysMessageEx(who, "Voce nao pode atacar depois de ter trocado a arma.", SSM_FAIL);
		return;
	endif

   TS_StartCooldown(who, "habilidade um", 20);
	who.hidden := 0;

   var esquiva:= GetObjProperty(targ, "evasao");

   if (esquiva == 1)
      PlayAttackAnimation(who);
      PlayMissSound(who);
      PrintText(targ, "*esquiva-se*");
      EraseObjProperty(targ, "evasao");
      return;
   endif

   var random := Randomint (100)+1;
   var hit_chance := CalculateHitChancehab(who, targ);

   if ( random > hit_chance )
      PlayMissSound(targ);
      return;
   endif

   var dmgList := dictionary;
   dmgList := getNewDamageList(who);
   var basedamage := CalculateRawDamage(who, who.weapon);
   var total := cint(CalculateTotalDamage(targ, basedamage, who, dmgList));

   if (!ParryCheck(who, targ, total));
      PlayAttackAnimation(who);
      PlaySoundEffect(who, cint(0x145));
      PrintText(who, "*Bate com o cabo da arma*");
      TS_StartTimer(targ, "tombar", 10);
      TS_StartTimer(targ, "lentidao", 4, who);
      TS_StartTimer(targ, "Paralysis", 3, who);  
      DamageFLS(targ, total, DMG_FORCED, who);
      PlayHitSounds(who, targ);
   else
      PlayHitSounds(who, targ);
      PlayAttackAnimation(who);
   endif
  
	return 1;
endprogram
